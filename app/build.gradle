buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.3.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.apollographql.apollo'
apply from: 'quality.gradle'

def externalPrivateVersion = new File(project.rootDir.path + "/app/external_version_code.txt").text.trim().toInteger()
def externalPublicVersion = new File(project.rootDir.path + "/app/external_version_name.txt").text.trim()
def internalPrivateVersion = new File(project.rootDir.path + "/app/internal_version_code.txt").text.trim().toInteger()
def internalPublicVersion = new File(project.rootDir.path + "/app/internal_version_name.txt").text.trim()
def isCircle = "true" == System.getenv("CIRCLECI")

ext.signing = [
        storeFilePath: "path/to/keystore",
        storePassword: "keystore password",
        keyAlias     : "key alias",
        keyPassword  : "key password"
]

if (file('signing.gradle').exists()) {
    apply from: 'signing.gradle'
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "com.kickstarter"
        minSdkVersion 23
        targetSdkVersion 29
        testApplicationId "com.kickstarter.kickstarter.regression.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "GIT_SHA", "\"${commitSha()}\""
        buildConfigField "String", "BUILD_DATE", "\"${buildDate()}\""

        // Enable Multidex to support over 65K methods
        multiDexEnabled true

        resConfigs "de", "es", "en", "fr", "ja"

        def fabricProps = new Properties()
        file("fabric.properties").withInputStream { fabricProps.load(it) }
        manifestPlaceholders = [apiKey: fabricProps.getProperty('apiKey'), performanceLogs: false]
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file(project.signing.storeFilePath)
            storePassword project.signing.storePassword
            keyAlias project.signing.keyAlias
            keyPassword project.signing.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            if (isCircle) {
                // The release build generated on CircleCI doesn't need to be signed with our real
                // keystore - we just need a release build to verify that it compiles. Using the
                // debug keystore means we don't have to expose our keystore.
                signingConfig signingConfigs.debug
            } else {
                signingConfig signingConfigs.release
            }
        }
        debug {
            minifyEnabled false
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "AUDIENCE"

    productFlavors {
        internal {
            dimension "AUDIENCE"
            applicationId "com.kickstarter.kickstarter.internal"
            versionCode internalPrivateVersion
            versionName internalPublicVersion
            manifestPlaceholders = [performanceLogs: true]
        }
        external {
            dimension "AUDIENCE"
            applicationId "com.kickstarter.kickstarter"
            versionCode externalPrivateVersion
            versionName externalPublicVersion
        }
        regression {
            dimension "AUDIENCE"
            applicationId "com.kickstarter.kickstarter.regression"
            versionCode externalPrivateVersion
            versionName externalPublicVersion
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    // Fixes build error hitting GC overhead limit
    dexOptions {
        javaMaxHeapSize "3072M"
    }

    lintOptions {
        disable 'NonConstantResourceId'
        quiet true
        warningsAsErrors true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
    }

    sourceSets {
        regression {
            androidTest.java.srcDirs = ['src/androidTestRegressionDebug/kotlin']
        }
        main.java.srcDirs += 'src/main/kotlin'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.android.support:appcompat-v7:27.1.1'
        resolutionStrategy.force 'com.android.support:animated-vector-drawable:27.1.1'
        resolutionStrategy.force 'com.android.support:customtabs:27.1.1'
    }

}

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.apollographql.apollo:apollo-gradle-plugin:2.3.0'
    }
}

apollo {
    customTypeMapping = [
            "Date" : "java.util.Date",
            "Email": "java.lang.String",
            "ISO8601DateTime" : "org.joda.time.DateTime",
            "DateTime" : "org.joda.time.DateTime"
    ]
}

repositories {
    jcenter()
    maven {
        url 'https://maven.google.com'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta01'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.work:work-runtime-ktx:2.3.4'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.apollographql.apollo:apollo-runtime:2.3.0'
    implementation 'com.facebook.android:facebook-android-sdk:7.1.0'
    final auto_parcel_version = "0.3.1"
    implementation "com.github.frankiesardo:auto-parcel:$auto_parcel_version"
    kapt "com.github.frankiesardo:auto-parcel-processor:$auto_parcel_version"
    implementation "com.google.android.gms:play-services-wallet:18.0.0"
    implementation "com.google.android.exoplayer:exoplayer:2.10.2"
    implementation 'com.google.android:flexbox:1.1.0'
    implementation 'com.google.android.material:material:1.1.0-alpha08'
    final dagger_version = "2.11"
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation "com.jakewharton:butterknife:7.0.1"
    implementation "com.jakewharton:process-phoenix:2.0.0"
    final rx_binding_version = "0.4.0"
    implementation "com.jakewharton.rxbinding:rxbinding:$rx_binding_version"
    implementation "com.jakewharton.rxbinding:rxbinding-recyclerview-v7:$rx_binding_version"
    implementation "com.jakewharton.rxbinding:rxbinding-support-v4:$rx_binding_version"
    implementation "com.jakewharton.timber:timber:3.0.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.optimizely.ab:android-sdk:3.0.0'
    implementation 'com.qualtrics:digital:1.3'
    implementation "com.stripe:stripe-android:12.0.1"
    final okhttp_version = "3.5.0"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttp_version"
    implementation "com.squareup.picasso:picasso:2.5.2"
    final retrofit_version = "2.3.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    final rx_lifecycle_version = "0.3.0"
    implementation "com.trello:rxlifecycle:$rx_lifecycle_version"
    implementation "com.trello:rxlifecycle-components:$rx_lifecycle_version"
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation "io.reactivex:rxjava:1.1.5"
    implementation "net.danlew:android.joda:2.7.2"
    implementation "org.jsoup:jsoup:1.12.1"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:26.0.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-perf-ktx'
    implementation "com.google.firebase:firebase-messaging"

    // Testing
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:2.8.9"
    testImplementation "org.robolectric:robolectric:4.3"
    testImplementation "org.robolectric:shadows-multidex:4.3"
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation "androidx.test:core:1.2.0"

    def espresso = '3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:' + espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:' + espresso
    androidTestImplementation 'androidx.test.espresso:espresso-intents:' + espresso
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation 'androidx.test:rules:1.2.0'
}

// SHA and timestamp caching courtesy of https://github.com/gdg-x/frisbee/blob/develop/app/build.gradle#L193-L218
def commitSha() {
    def file = new File(project.buildDir, "commit-sha.txt")
    if (file.exists()) {
        return file.text
    }

    def value = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

    file.parentFile.mkdirs()
    file.text = value
    return value
}

def buildDate() {
    def file = new File(project.buildDir, "build-date.txt")

    def date = new Date()
    def value = date.format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))

    file.parentFile.mkdirs()
    file.text = value
    return value
}

// Copy google-services.json from variant directory to root of app
gradle.taskGraph.beforeTask { Task task ->
    if (task.name ==~ /process.*GoogleServices/) {
        android.applicationVariants.all { variant ->
            if (task.name ==~ /(?i)process${variant.name}GoogleServices/) {
                copy {
                    from "src/${variant.name}"
                    into '.'
                    include 'google-services.json'
                }
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
