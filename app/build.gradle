buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.apollographql.apollo'
apply from: 'quality.gradle'
apply from: "$project.rootDir/jacoco.gradle"

def externalPrivateVersion = new File(project.rootDir.path + "/app/external_version_code.txt").text.trim().toInteger()
def externalPublicVersion = new File(project.rootDir.path + "/app/external_version_name.txt").text.trim()
def internalPrivateVersion = new File(project.rootDir.path + "/app/internal_version_code.txt").text.trim().toInteger()
def internalPublicVersion = new File(project.rootDir.path + "/app/internal_version_name.txt").text.trim()
def isCircle = "true" == System.getenv("CIRCLECI")
def isPrivateCIEnv = ("android-private" == System.getenv("CIRCLE_PROJECT_REPONAME"))
def isRelease = ("external" == System.getenv("CIRCLE_BRANCH"))

ext.signing = [
        storeFilePath: "path/to/keystore",
        storePassword: "keystore password",
        keyAlias     : "key alias",
        keyPassword  : "key password"
]

if (file('signing.gradle').exists()) {
    apply from: 'signing.gradle'
}



android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.kickstarter"
        minSdkVersion 23
        targetSdkVersion 31
        testApplicationId "com.kickstarter.internal.debug.test"
        testInstrumentationRunner "com.kickstarter.screenshoot.testing.KSScreenShotTestRunner"

        buildConfigField "String", "GIT_SHA", "\"${commitSha()}\""
        buildConfigField "String", "BUILD_DATE", "\"${buildDate()}\""

        // Enable Multidex to support over 65K methods
        multiDexEnabled true
        resConfigs 'de', 'es', 'en', 'fr', 'ja'


        def fabricProps = new Properties()
        file("fabric.properties").withInputStream { fabricProps.load(it) }
        manifestPlaceholders = [apiKey: fabricProps.getProperty('apiKey'), performanceLogs: false]
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file(project.signing.storeFilePath)
            storePassword project.signing.storePassword
            keyAlias project.signing.keyAlias
            keyPassword project.signing.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            if (isCircle && isPrivateCIEnv && isRelease) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
        debug {
            minifyEnabled false
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
            FirebasePerformance {
                // Set this flag to 'false' to disable @AddTrace annotation processing and
                // automatic monitoring of HTTP/S network requests
                // for a specific build variant at compile time.
                instrumentationEnabled false
            }
        }
    }

    flavorDimensions "AUDIENCE"

    productFlavors {
        internal {
            dimension "AUDIENCE"
            applicationId "com.kickstarter.kickstarter.internal"
            versionCode internalPrivateVersion
            versionName internalPublicVersion
            manifestPlaceholders = [performanceLogs: false]
        }
        external {
            dimension "AUDIENCE"
            applicationId "com.kickstarter.kickstarter"
            versionCode externalPrivateVersion
            versionName externalPublicVersion
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    buildFeatures {
        compose false
        viewBinding true
    }


    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
        resources {
            excludes += ['META-INF/LICENSE.txt', 'LICENSE.txt', 'META-INF/AL2.0', 'META-INF/LGPL2.1']
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if( names.contains("regression")) {
            setIgnore(true)
        }
    }
    lint {
        abortOnError false
        warningsAsErrors true
    }
    namespace 'com.kickstarter'

}

buildscript {
    repositories {
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.apollographql.apollo:apollo-gradle-plugin:2.5.11'
        classpath 'com.karumi:shot:5.13.0'
    }
}

apollo {
    customTypeMapping = [
            "Date" : "java.util.Date",
            "Email": "java.lang.String",
            "ISO8601DateTime" : "org.joda.time.DateTime",
            "DateTime" : "org.joda.time.DateTime"
    ]
}

repositories {
    // This is the only reference we cannot remove yet as it has not been migrated to mavenCentral() or google()
    // see - https://github.com/google/flexbox-layout/issues/566
    maven {
        url 'https://maven.google.com'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation 'com.apollographql.apollo:apollo-runtime:2.5.11'
    implementation 'com.facebook.android:facebook-android-sdk:12.0.1'
    implementation 'com.google.android.play:core-ktx:1.8.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "com.google.android.exoplayer:exoplayer:2.17.1"
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.google.android.material:material:1.6.1'
    final dagger_version = "2.41"
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation 'com.jakewharton:process-phoenix:2.1.2'
    final rx_binding_version = '1.0.1'
    implementation "com.jakewharton.rxbinding:rxbinding:$rx_binding_version"
    implementation "com.jakewharton.rxbinding:rxbinding-recyclerview-v7:$rx_binding_version"
    implementation "com.jakewharton.rxbinding:rxbinding-support-v4:$rx_binding_version"
    implementation "com.jakewharton.timber:timber:5.0.1"
    implementation 'com.optimizely.ab:android-sdk:3.13.2'
    implementation 'com.stripe:stripe-android:20.5.0'
    final okhttp_version = '4.10.+'
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttp_version"

    final retrofit_version = "2.9.+"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    final rx_lifecycle_version = "0.3.0"
    implementation "com.trello:rxlifecycle:$rx_lifecycle_version"
    implementation "com.trello:rxlifecycle-components:$rx_lifecycle_version"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'net.danlew:android.joda:2.10.7'
    implementation "org.jsoup:jsoup:1.14.3"

    // Data Store (SharedPreferences like API)
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Analytics Segment-Braze
    implementation 'com.segment.analytics.android:analytics:4.10.4'
    implementation 'com.appboy:appboy-segment-integration:10.0.0'

    // Security
    implementation 'com.perimeterx.sdk:msdk:1.16.7'

    // Dependencies for HTML Parser
    implementation 'org.jsoup:jsoup:1.14.3'

    // Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    // GLIDE
    implementation 'com.github.bumptech.glide:glide:4.13.1'
    kapt 'com.github.bumptech.glide:compiler:4.13.1'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:29.2.1')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation "com.google.firebase:firebase-messaging"
    implementation 'com.google.firebase:firebase-inappmessaging-display'
    implementation 'com.google.firebase:firebase-perf'

    // Zoom on Images
    implementation 'io.github.aghajari:ZoomHelper:1.1.0'

    // Testing
    testImplementation "junit:junit:4.13.2"
    testImplementation 'org.mockito:mockito-core:4.5.1'
    testImplementation 'org.robolectric:robolectric:4.8'
    testImplementation "androidx.test:core:1.4.0"
    androidTestImplementation 'androidx.annotation:annotation:1.3.0'

    def espresso = '3.4.0'
    androidTestImplementation ('androidx.test.espresso:espresso-contrib:' + espresso) {
        exclude module: "protobuf-lite"
    }
    androidTestImplementation 'androidx.test.espresso:espresso-core:' + espresso
    androidTestImplementation 'androidx.test.espresso:espresso-intents:' + espresso
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation 'androidx.test:rules:1.4.0'
}

// SHA and timestamp caching courtesy of https://github.com/gdg-x/frisbee/blob/develop/app/build.gradle#L193-L218
def commitSha() {
    def file = new File(project.buildDir, "commit-sha.txt")
    if (file.exists()) {
        return file.text
    }

    def value = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

    file.parentFile.mkdirs()
    file.text = value
    return value
}

def buildDate() {
    def file = new File(project.buildDir, "build-date.txt")

    def date = new Date()
    def value = date.format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))

    file.parentFile.mkdirs()
    file.text = value
    return value
}

// Copy google-services.json from variant directory to root of app
gradle.taskGraph.beforeTask { Task task ->
    if (task.name ==~ /process.*GoogleServices/) {
        android.applicationVariants.all { variant ->
            if (task.name ==~ /(?i)process${variant.name}GoogleServices/) {
                copy {
                    from "src/${variant.name}"
                    into '.'
                    include 'google-services.json'
                }
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'

apply plugin: 'shot'
shot {
    useComposer = false
}