apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.google.gms.google-services'
apply from: 'quality.gradle'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def date = new Date();
def buildDate = date.format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))
def privateVersion = new File(project.rootDir.path + "/app/version_code.txt").text.trim().toInteger()
def publicVersion = new File(project.rootDir.path + "/app/version_name.txt").text.trim()
def isCircle = "true".equals(System.getenv("CIRCLECI"))

ext.signing = [
    storeFilePath : "path/to/keystore",
    storePassword : "keystore password",
    keyAlias      : "key alias",
    keyPassword   : "key password"
]

if (file('signing.gradle').exists()) {
    apply from: 'signing.gradle'
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.kickstarter"
        minSdkVersion 16
        targetSdkVersion 23
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_DATE", "\"${buildDate}\""

        // Version code is an internal/private version number
        versionCode privateVersion
        // Version name is shown to users
        versionName publicVersion

        // Enable Multidex to support over 65K methods
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file(project.signing.storeFilePath)
            storePassword project.signing.storePassword
            keyAlias project.signing.keyAlias
            keyPassword project.signing.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            if (isCircle) {
                // The release build generated on CircleCI doesn't need to be signed with our real
                // keystore - we just need a release build to verify that it compiles. Using the
                // debug keystore means we don't have to expose our keystore.
                signingConfig signingConfigs.debug
            } else {
                signingConfig signingConfigs.release
            }
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "AUDIENCE", "API"

    productFlavors {
        pre21 {
            dimension "API"
            // Don't override the minSdkVersion, just inherit from defaultConfig
        }
        min21 {
            // minSdkVersion 21 speeds up local development by generating multidex output much faster.
            // It should not be used for release builds. See:
            // https://developer.android.com/tools/building/multidex.html#dev-build
            dimension "API"
            minSdkVersion 21
        }
        internal {
            dimension "AUDIENCE"
            applicationId "com.kickstarter.kickstarter.internal"
        }
        external {
            dimension "AUDIENCE"
            applicationId "com.kickstarter.kickstarter"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Fixes build error hitting GC overhead limit
    dexOptions {
        javaMaxHeapSize "2560M"
    }

    lintOptions {
        disable 'InvalidPackage'
        disable 'MissingTranslation'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
    }

    sourceSets {
        androidTest {
            java.srcDir file('src/androidTest')
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:support-annotations:23.1.1'
    compile 'com.facebook.android:facebook-android-sdk:4.7.0'
    compile 'com.github.frankiesardo:auto-parcel:0.3.1'
    apt 'com.github.frankiesardo:auto-parcel-processor:0.3.1'
    compile 'com.google.android.gms:play-services-gcm:8.1.0'
    compile 'com.google.android.exoplayer:exoplayer:r1.5.5'
    compile 'com.google.dagger:dagger:2.0'
    apt 'com.google.dagger:dagger-compiler:2.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton:process-phoenix:1.0.0'
    compile 'com.jakewharton.timber:timber:3.0.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.1.2'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.1.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta4'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta4'
    compile 'com.trello:rxlifecycle:0.3.0'
    compile 'com.trello:rxlifecycle-components:0.3.0'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-support-v4:0.3.0'
    compile 'net.danlew:android.joda:2.7.2'
    compile 'net.hockeyapp.android:HockeySDK:3.6.1'
    ***REMOVED***
    compile 'org.jsoup:jsoup:1.8.2'
    compile "com.mixpanel.android:mixpanel-android:4.6.4"

    // Testing
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.robolectric:shadows-multidex:3.0'
    testCompile "org.robolectric:shadows-play-services:3.0"
    testCompile "org.robolectric:shadows-support-v4:3.0"
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
    androidTestCompile 'com.android.support.test:rules:0.3'
    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support:support-annotations:23.1.1'
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.3'
    }
}

// Copy google-services.json from variant directory to root of app
gradle.taskGraph.beforeTask { Task task ->
    if (task.name ==~ /process.*GoogleServices/) {
        android.applicationVariants.all { variant ->
            if (task.name ==~ /(?i)process${variant.name}GoogleServices/){
                copy {
                    from "src/${variant.name}"
                    into '.'
                    include 'google-services.json'
                }
            }
        }
    }
}


apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
